# For each protein, check it's presence in the available dbs
PREFIX schema: <https://schema.org/>
PREFIX idp: <https://idpcentral.org/registry/>

SELECT ?UniProtID (MAX(?mobidb) as ?MobiDB) (MAX(?ped) as ?PED) (MAX(?disprot) as ?Disprot)
WHERE {
    {
        SELECT ?UniProtID ?mobidb ?ped ?disprot
        WHERE {
            GRAPH ?g {
                ?x schema:sameAs ?UniProtID .
            }
            BIND((?g = idp:ped) AS ?ped)
            BIND((?g = idp:disprot) AS ?disprot)
            BIND((?g = idp:mobidb) AS ?mobidb)

            {
                SELECT DISTINCT ?UniProtID
                WHERE {
                    ?protein a schema:Protein ;
                        schema:sameAs ?UniProtID .
                }
            }
        }
    }
}
GROUP BY ?UniProtID
