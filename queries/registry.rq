# Get a Uniprot protein-centric view of all the annotations associated to it (PED, MobiDB and DisProt), with informations about the annotation and start-end positions. The name of the organism is fetched from Uniprot via the Uniprot URI

PREFIX schema: <https://schema.org/>
PREFIX idp: <https://idpcentral.org/registry/>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX up: <http://purl.uniprot.org/core/>

SELECT DISTINCT ?sequenceID ?organismName ?name ?taxon ?source ?sourceID ?start ?end ?annotationName ?annotationCode
WHERE {
    {
        graph idp:disprot {
            ?protein schema:sameAs ?sequenceID ;
                schema:hasSequenceAnnotation ?annotationID ;
                schema:name ?name ;
                schema:identifier ?identifier ;
                dc:title ?source
            OPTIONAL {
                ?protein schema:taxonomicRange/schema:termCode ?taxon
            }
        }
    } UNION {
        graph idp:mobidb {
            ?protein schema:sameAs ?sequenceID ;
                schema:hasSequenceAnnotation ?annotationID ;
                schema:name ?name ;
                schema:identifier ?identifier ;
                dc:title ?source .

            OPTIONAL {
                ?protein schema:taxonomicRange/schema:termCode ?taxon
            }
        }
    } UNION {
        graph idp:ped {
            ?collection a schema:CollectionPage ;
                dc:title ?source ;
                schema:mainEntity ?e ;
                schema:identifier ?identifier .
            ?e schema:itemListElement ?protein .
            ?protein schema:sameAs ?sequenceID ;
                schema:name ?name ;
                schema:hasSequenceAnnotation ?annotationID ;
        }
    }
    BIND(REPLACE(?identifier, "(^.+:)", "") AS ?sourceID)

    ?annotationID schema:sequenceLocation ?sequenceLocation ;
        schema:additionalProperty/schema:value ?annotation .
    ?sequenceLocation schema:rangeStart ?start ;
        schema:rangeEnd ?end .
    ?annotation schema:name ?annotationName ;
        schema:termCode ?annotationCode .

    {
        SELECT DISTINCT ?sequenceID ?organismName
        WHERE {
            {
                SELECT DISTINCT ?sequenceID
                WHERE {
                    ?protein a schema:Protein ;
                        schema:sameAs ?sequenceID .
                }
                OFFSET 0
                LIMIT 3
            }

            SERVICE <https://sparql.uniprot.org/sparql> {
                ?sequenceID a up:Protein ;
                    up:organism ?organism .
                ?organism up:scientificName ?organismName .
            }
        }
    }
}
ORDER BY ?source ?start ?end
